         TITLE 'TSOENQ - LIST DATASET USERS'
*=====================================================================*
* THIS PROGRAM REQUIRES A DATASET NAME FROM THE USER. IT SCANS THE    *
* GRS SERIALIZATION QUEUE FOR HITS ON THIS MINOR NAME.                *
* ENQUEUES SEARCHED IS : STEP, SYSTEM, SYSTEMS W/IN THE CEC WE EXECUTE*
* MULTIPLE MACHINES CAN BE SUPPORTED BY USING SCOPE=GLOBAL.           *
*=====================================================================*
         SPACE
TSOENQ   CSECT
         USING *,R15
         B     AROUND
         DC    AL1(23)
         DC    CL9'&SYSDATE'
         DC    CL5'&SYSTIME'
AROUND   DS    0H
         STM   R14,R12,12(R13)
         ST    R13,SAVEAREA+4
         LA    R7,SAVEAREA
         ST    R7,8(R13)
         LA    R13,SAVEAREA
         DROP  R15
         USING SAVEAREA,R13
         B     BEGIN
SAVEAREA DC    18F'0'
BEGIN    DS    0H
         L     R1,0(R1)
         LH    R2,0(R1)                R2 ==> DSN LENGTH
         BCTR  R2,0
         STC   R2,MVC1+1               ALTER MOVES
         STC   R2,MVC2+1               *
         LR    R3,R2                   MAKE DASHES
         LA    R3,11(R3)               FLOAT
         STC   R3,MVC3+1               *
         LA    R2,1(R2)                MAKE DSN L' REAL
         ST    R2,DSNL                 SAVE DSNAME L'
MVC1     MVC   RNAME(0),2(R1)          DSNAME IS MINOR
* GET 1 MB
         GETMAIN EC,LV=1024000,A=@MYRIB
         LTR   R15,R15                 DID WE GET IT ?
         BZ    MAINOK                  YES
         BAL   R11,MAINERR             NO..ABEND W/ MSG
MAINOK   EQU   *
*
         OPEN  (SYSPRINT,(OUTPUT))
         LOCATE CAMLST                 FIND DSNAME LOCATION
         LTR   R15,R15                 OK ?
         BZ    MVC2                    YES..
         MVC   VOLSERS+6(6),=6C'*'     MARK AS NOT CATALOGUED
MVC2     MVC   DETAIL(0),RNAME         TARGET TO PRT
         LA    R15,DETAIL(R2)
         MVI   3(R15),C'('
         MVC   4(6,R15),VOLSERS+6      VOLSER LOCATION TO PRINT
         MVI   10(R15),C')'
         BAL   R11,PRINTIT             OUTPUT 1ST LINE
MVC3     MVC   DETAIL(0),DASHES        SEPARATOR
         BAL   R11,PRINTIT             OUTPUT 2ND LINE
         L     R2,DSNL                 RELOAD DSNAME L'
         L     R3,RIBLEN               GETMAIN L'
         L     R5,@MYRIB               R4==>GQSCAN OUTPUT ADR
         XC    TOKEN(L'TOKEN),TOKEN    CLEAR TOKEN FILED
         MODESET MODE=SUP
         GQSCAN AREA=((R5),(R3)),                                      X
               REQLIM=MAX,                                             X
               SCOPE=ALL,                                              X
               RESNAME=(QNAME,RNAME,(R2),SPECIFIC,0),                  X
               TOKEN=TOKEN
         STM   R15,R0,SAVE15           SAVE RC/RS
*************************************
* PRINT RC (TEST ONLY)
*        UNPK  RCMSG+3(3),SAVE15+2(2)  RC
*        MVZ   RCMSG+3(2),=2X'00'      ZONES OFF
*        TR    RCMSG+3(2),TABLE        MAKE PRINTABLE
*        MVC   DETAIL(L'RCMSG-1),RCMSG MSG TO PRT
*        BAL   R11,PRINTIT             PRINT THE LINE
* END PRINT RC (TEST ONLY)
*************************************
         MODESET MODE=PROB
         LM    R15,R0,SAVE15           RESTORE RC/RS
         SLL   R15,2                   * 4
         B     BRTAB(R15)
BRTAB    B     SOMFOUND                RC=0
         B     NONFOUND                RC=4
         B     GQERR                   RC=8
         B     GQERR                   RC=12
         B     NONFOUND                RC=16
         B     GQERR                   RC=20
SOMFOUND EQU   *
         LR    R2,R0
         LR    R3,R0
         SRL   R2,16                   R2 ==> L' RIB FIXED AREA
         SLL   R3,16
         SRL   R3,16                   R3 ==> L' RIBE
         L     R5,@MYRIB               R5 ==> A(RIB)
         USING RIB,R5
         MVC   MAJORNAM(8),RIBQNAME    SAVE MAJOR
         L     R4,RIBNRIBE             R4 ==> # RIBE BLOCKS
         LH    R15,RIBVLEN             R15 ==> RIB VARIABLE L'
         AR    R5,R2                   ADD IN RIB FIXED L'
         AR    R5,R15                  R5 ==> 1ST RIBE
         USING RIBE,R5
INNER    EQU   *
         MVC   DETAIL(8),RIBEJBNM      REQUESTOR JOBNAME
         TM    RIBERFLG,RIBERESV       RESERVE ?
         BO    RESERVE                 YES
         TM    RIBERFLG,RIBETYPE       ENQ SHR ?
         BO    SHARE                   YES
         MVC   DETAIL+8+3(3),=C'EXC'   MARK EXCLUSIVE ENQ
         B     MAJOR
RESERVE  EQU   *
         MVC   DETAIL+8+3(3),=C'RSV'   MARK RESERVE
         B     MAJOR
SHARE    EQU   *
         MVC   DETAIL+8+3(3),=C'SHR'   MARK SHARED ENQ
         SPACE
MAJOR    EQU   *
         MVC   DETAIL+8+3+6(8),MAJORNAM MAJOR ENQ NAME TO DETAIL
         MVI   DETAIL+8+3+6+11,C'('    (
         MVC   DETAIL+8+3+6+11+1(4),RIBESYSN  SYSTEM NAME
         MVI   DETAIL+8+3+6+11+5,C')'   )
         BAL   R11,PRINTIT             PRINT DETAIL LINE
         LA    R5,0(R3,R5)             POINT TO NEXT RIBE
         BCT   R4,INNER                DO ALL ENQ/RSV'S
         B     EXIT
         SPACE
NONFOUND EQU   *
         MVC   DETAIL(19),=C'*== NO ENQUEUES ==*'
         BAL   R11,PRINTIT             NO..POINT THAT OUT
EXIT     EQU   *
         FREEMAIN EU,LV=1024000,A=@MYRIB
         CLOSE (SYSPRINT)
         L     R13,SAVEAREA+4          EXIT BACK TO TSO
         RETURN (14,12),RC=0
         EJECT
*=====================================================================*
*                         S U B R O U T I N E S                       *
*=====================================================================*
         SPACE
         DC    CL8'PRINTIT',F'0'
PRINTIT  ST    R11,PRINTIT-4
         PUT   SYSPRINT,DETAIL
         MVC   DETAIL(L'DETAIL),DETAIL-1
         L     R11,PRINTIT-4
         BALR  R11,R11
         SPACE 3
FINDERR  EQU  *
         CVD   R15,DOUB
         UNPK  FINDMSG+20(4),DOUB+6(2)
         OI    FINDMSG+23,240
         MVC   FINDMSG+29(44),RNAME
         MVC   DETAIL(L'FINDMSG),FINDMSG
         BAL   R11,PRINTIT
         B     EXIT
         SPACE 3
GQERR    EQU  *
         CVD   R15,DOUB
         UNPK  GQMSG+16(4),DOUB+6(2)
         OI    GQMSG+19,240
         MVC   DETAIL(L'GQMSG),GQMSG
         BAL   R11,PRINTIT
         B     EXIT
         SPACE 3
MAINERR  EQU   *
         CVD   R15,DOUB                GET RC
         UNPK  MSGMAIN+16(4),DOUB+6(2) RC TO MSG
         OI    MSGMAIN+19,240
         XR    R14,R14
         L     R15,RIBLEN
         CVD   R15,DOUB
         MVC   MSGMAIN+26(L'MASKVOL),MASKVOL
         ED    MSGMAIN+26(L'MASKVOL),DOUB+3
         MVC   DETAIL+1(L'MSGMAIN),MSGMAIN    MSG TO ERROR RPT
         BAL   R11,PRINTIT             PRINT ERROR
         B     EXIT
         SPACE 3
         LTORG
         EJECT
*=====================================================================*
*                         D A T A   A R E A S                         *
*=====================================================================*
         SPACE
DOUB     DS    D
MAJORNAM DC    CL8' '
QNAME    DC    8X'00'              SHOULD ALWAYS BE NULL
RNAME    DC    CL44' '             DATASET IN QUESTION
TOKEN    DC    F'0'
DSNL     DS    F
SAVE15   DS    2F                  R15,R0
FOUND    DC    C'0'
TABLE    DC    C'0123456789ABCDEF'
CAMLST   CAMLST NAME,RNAME,,VOLSERS
FINDMSG  DC    CL80'DSNAME NOT FOUND,RC=XXXX,DSN='
GQMSG    DC    C'ENQ SCAN ERR,RC=XXXX'
RCMSG    DC    C'RC=XXX'
         DC    C' '
DETAIL   DC    CL80' '
DASHES   DC    55C'-'
         DS    0D
         PRINT NOGEN
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,BLKSIZE=80
         PRINT GEN
         DS    0D
VOLSERS  DC    265C' '
         DC    C'MYRIB'
@MYRIB   DC    A(0)
RIBLEN   DC    A(1024000)                   1 MEG
MASKVOL  DC    X'402020202020202120'
MSGMAIN  DC    CL34'GETMAIN ERR,R15=XXXX,SIZE=XXXXXXXXX'
         EJECT
         ISGRIB
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
